plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootPluginVersion}"
    id 'io.spring.dependency-management' version "${springDepManPluginVersion}"
    id 'org.springframework.cloud.contract' version "${springCloudContractPluginVersion}"
    id 'maven-publish'
}

group = 'io.github.projectleopold.demo'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

contracts {
    testFramework = 'JUNIT5'
    basePackageForTests = 'io.github.projectleopold.demo.springproducer'
    baseClassForTests = 'io.github.projectleopold.demo.springproducer.BaseRestContractTest'
}

publishing {
    publications {
        stubs(MavenPublication) {
            artifact verifierStubsJar
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/projectleopold/demo-spring-producer'
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
